&НаСервере
Функция ПодготовитьТелоЗапроса(ИдентификаторУстройства)Экспорт
	
	Узел = ПланыОбмена.ОбменСЦентральнойБазой.НайтиПоКоду(ИдентификаторУстройства);
	
	Если НЕ ЗначениеЗаполнено(Узел) Тогда
		УзелОбъект = ПланыОбмена.ОбменСЦентральнойБазой.СоздатьУзел();
		
		УзелОбъект.Наименование = "Центральная база";
		УзелОбъект.Код = ИдентификаторУстройства;
		УзелОбъект.Записать();
		Узел = УзелОбъект.Ссылка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
					|	ДоставкаИзменения.Ссылка КАК Ссылка,
					|	ДоставкаИзменения.Ссылка.Комментарий КАК Комментарий,
					|	ДоставкаИзменения.Ссылка.Фото КАК Фото,
					|	ДоставкаИзменения.Ссылка.Статус КАК Статус
					|ИЗ
					|	Документ.Доставка.Изменения КАК ДоставкаИзменения
					|ГДЕ
					|	ДоставкаИзменения.Узел = &Узел";
	
	Запрос.УстановитьПараметр("Узел", Узел);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	КонецЕсли;

	ДанныеДляОтправки = Новый Массив;
	ОтправленныеОбъекты = Новый Массив;
	
//Добавляем документы для отправки
	Выборка = РезультатЗапроса.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Идентификатор", Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ДанныеДокумента.Вставить("Комментарий", Выборка.Комментарий);
		
		СтрокаФото = Base64Строка(Выборка.Фото);
		ДанныеДокумента.Вставить("Фото", СтрокаФото);
		
		СтрокаСтатус = XMLЗначение(Выборка.Статус, "Статус");
		ДанныеДокумента.Вставить("Статус", СтрокаСтатус);
		
		ДанныеДляОтправки.Добавить(ДанныеДокумента);
		
		ОтправленныеОбъекты.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	ТелоОтвета = ЗаписатьЗначениеJSON(ДанныеДляОтправки);
	
	Если ЗначениеЗаполнено(ОтправленныеОбъекты) Тогда
		ПланыОбмена.УдалитьРегистрациюИзменений(Узел, ОтправленныеОбъекты);
	КонецЕсли;
	
	Возврат ТелоОтвета
	
КонецФункции
