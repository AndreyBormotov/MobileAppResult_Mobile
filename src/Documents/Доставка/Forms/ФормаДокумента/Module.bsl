#Область обработчикиСобытийФормы
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(АдресФото) Тогда
		ДвоичныеДанныеФото = ПолучитьИзВременногоХранилища(АдресФото);
		ТекущийОбъект.Фотография = Новый ХранилищеЗначения(ДвоичныеДанныеФото);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДвоичныеДанныеФото = ТекущийОбъект.Фотография.Получить();
	АдресФото = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФото, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если (Объект.Статус = Перечисления.СтатусыДоставок.ОтклоненКлиентом ИЛИ
			Объект.Статус = Перечисления.СтатусыДоставок.ОтклоненКурьером) И
			НЕ ЗначениеЗаполнено(Объект.Комментарий) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Поле = "Объект.Комментарий";
			Сообщение.Текст = "Необходимо заполнить поле комментарий";
			Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти



#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ФотоНажатие(Элемент, СтандартнаяОбработка)

	#Если МобильноеПриложениеКлиент Тогда
		
	СтандартнаяОбработка = Ложь;
		
	Если СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
		НовоеФото = СредстваМультимедиа.СделатьФотоснимок();
		Если НовоеФото = Неопределено Тогда
			Возврат
		КонецЕсли;
		ДвоичныеДанные = НовоеФото.ПолучитьДвоичныеДанные();
		АдресФото = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	КОнецЕсли;
		
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура Позвонить(Команда)
	#Если МобильноеПриложениеКлиент Тогда
	Если СредстваТелефонии.ПоддерживаетсяНаборНомера() Тогда
		СредстваТелефонии.НабратьНомер(Объект.КонтактныйТелефон, Истина);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры
#КонецОбласти